<!-- A little <code>s///</code> and <code>m//</code> -->


<br />
<!::field::date::><br />
<p><table
width="180" cellspacing="0" cellpadding="4" align="right" border="1">
<tr> <td align="center" valign="top" bgcolor="#6699cc"> <font
color="#ffffff">Table of Contents</td> </tr> <tr> <td> <p
class="fine"> <b><a
href="http://www.perl.com/pub/2000/10/begperl1.html">Part 1 of this
series</a></b><br /> <b><a
href="http://www.perl.com/pub/2000/11/begperl2.html">Part 2 of this
series</b></a><br /> &#149;<a href="#simple matching">Simple
matching</a><br /> &#149;<a
href="#metacharacters">Metacharacters</a><br /> &#149;<a
href="#character classes">Character classes</a><br /> &#149;<a
href="#flags">Flags</a><br /> &#149;<a
href="#subexpressions">Subexpressions</a><br /> &#149;<a href="#watch
out!">Watch out!</a><br /> &#149;<a href="#search and replace">Search
and replace</a><br /> &#149;<a href="#play around!">Play around!</a>
</td> </tr> </p> </table> <p>We've covered flow control,

math and string operations, and files in the first two articles in this series.  Now we'll look at Perl's
most powerful and interesting way of playing with strings,
<em>regular expressions</em>, or <em>regexes</em> for short.  (The rule is this:
after the 50th time you type ``regular expression,'' you find you
type ``regexp'' the next 50 times.)</p> <p>Regular expressions are complex enough that you could write a whole
book on them (and, in fact, someone did - <em>Mastering Regular
Expressions</em> by Jeffrey Friedl).</p> <h3><a name="simple matching">Simple matching</a></h3> <p>The simplest regular expressions are <em>matching</em> expressions.  They
perform tests using keywords like <code>if</code>, <code>while</code> and
<code>unless</code>. Or, if you want to be really clever, tests that you can use with
<code>and</code> and <code>or</code>.  A matching regexp will return a true value if whatever
you try to match occurs inside a string.  When you want to
use a regular expression to match against a string, you use the special
<code>=~</code> operator:</p> <pre> $user_location = &quot;I see thirteen black cats under a ladder.&quot;;
    if ($string =~ /thirteen/) {
        print &quot;Eek, bad luck!\n&quot;;
    }
	</pre> <p>Notice the syntax of a regular expression: a string within a pair of slashes.
The code <code>$string =~ /thirteen/</code> asks whether the literal
string <code>thirteen</code> occurs anywhere inside <code>$string</code>.  If it does,
then the test evaluates true; otherwise, it evaluates false.</p>  <h3><a name="metacharacters">Metacharacters</a></h3> <p>A <em>metacharacter</em> is a character or sequence of characters that has special
meaning.  We've discussed metacharacters in the context of
double-quoted strings, where the sequence <code>\n</code> mean the newline character,
not a backslash, and the character <code>n</code> and <code>\t</code> means the tab character.</p> <p>Regular expressions have a rich vocabulary of metacharacters that let
you ask interesting questions such as, ``Does this expression occur at the end
of a string?'' or ``Does this string contain a series of numbers?''</p> <p>The two simplest metacharacters are <code>^</code> and <code>$</code>.  These indicate
``beginning of string'' and ``end of string,'' respectively.  For example,
the regexp <code>/^Bob/</code> will match ``Bob was here,'' ``Bob'' and ``Bobby.''  It
won't match ``It's Bob and David,'' because Bob doesn't show up at the
beginning of the string.  The <code>$</code> character, on the other hand, means
that you are matching the end of a string.  The regexp <code>/David$/</code> will
match ``Bob and David,'' but not ``David and Bob.''  Here's a simple routine
that will take lines from a file and only print URLs that seem to indicate
HTML files:</p> <pre>for $line (&lt;URLLIST&gt;) {
        # &quot;If the line starts with http: and ends with html....&quot;
        if (($line =~ /^http:/) and
            ($line =~ /html$/)) {
            print $line;
        }
    }</pre> <p>Another useful set of metacharacters is called <em>wildcards</em>.  If you've
ever used a Unix shell or the Windows DOS prompt, you're familiar with
wildcards characters like <code>*</code> and <code>?</code>.  For example when you type <code>ls a*.txt</code>, you see all filenames that begin with the letter <code>a</code> and
end with <code>.txt</code>.  Perl is a bit more complex, but works
on the same general principle.</p> <p>In Perl, the generic wildcard character is <code>.</code>.  A period inside a
regular expression will match <em>any</em> character, except a newline.  For
example, the regexp <code>/a.b/</code> will match anything that contains <code>a</code>,
another character that's not a newline, followed by <code>b</code> - ``aab,'' ``a3b,''
``a b,'' and so forth.</p> <p>If you want to <em>literally</em> match a metacharacter, you must escape it
with a backslash.  The regex <code>/Mr./</code> matches anything
that contains ``Mr'' followed by another character.  If you only want
to match a string that actually contains ``Mr.,'' you must use
<code>/Mr\./</code>.</p> <p>On its own, the <code>.</code> metacharacter isn't very useful, which is why Perl
provides three wildcard <em>quantifiers</em>:  <code>+</code>, <code>?</code> and <code>*</code>.  Each
quantifier means something different.</p> <p>The <code>+</code> quantifier is the easiest to understand: It means to match the
immediately preceding character or metacharacter <em>one or more times</em>.
The regular expression <code>/ab+c/</code> will match ``abc,'' ``abbc,'' ``abbbc'' and
so on.</p> <p>The <code>*</code> quantifier matches the immediately preceding character or
metacharacter <em>zero or more times</em>.  This is different from the <code>+</code> quantifier!  <code>/ab*c/</code> will match ``abc,'' ``abbc,'' and so on, just like
<code>/ab+c/</code> did, but it'll also match ``ac,'' because there are zero
occurences of <code>b</code> in that string.</p> <p>Finally, the <code>?</code> quantifier will match the preceding character <em>zero
or one times</em>.  The regex <code>/ab?c/</code> will match ``ac'' (zero occurences
of <code>b</code>) and ``abc'' (one occurence of <code>b</code>).  It won't match ``abbc,'' ``abbbc''
and so on.</p> <p>We can rewrite our URL-matching code to use these metacharacters.  This'll
make it more concise.  Instead of using two separate
regular expressions (<code>/^http:/</code> and <code>/html$/</code>), we combine them
into one regular expression:  <code>/^http:.+html$/</code>.  To understand
what this does, read from left to right: This regex will match any
string that <em>starts with ``http:''</em> followed by <em>one or more occurences
of any character</em>, and <em>ends with ``html''</em>.  Now, our routine is:</p> <pre> for $line (&lt;URLLIST&gt;) {
        if ($line =~ /^http:.+html$/) {
           print $line;
        }
    }</pre> <p>Remember the <code>/^something$/</code> construction - it's very useful!</p> <h3><a name="character classes">Character classes</a></h3> <p>We've already discussed one special metacharacter, <code>.</code>, that matches
any character except a newline.  But you'll often want to match
only specific types of characters.  Perl provides several metacharacters
for this.  &lt;\d&gt; will match a single digit, <code>\w</code> will match any single
``word'' character (which, to Perl, means a letter, digit or underscore),
and <code>\s</code> matches a whitespace character (space and tab, as well as the
<code>\n</code> and <code>\r</code> characters).</p> <p>These metacharacters work like any other character: You can match
against them, or you can use quantifiers like <code>+</code> and <code>*</code>.  The regex
<code>/^\s+/</code> will match any string that begins with whitespace, and <code>/\w+/</code> will match a string that contains at least one word.  (But remember that
Perl's definition of ``word'' characters includes digits and the underscore,
so whether or not you think <code>_</code> or <code>25</code> are words, Perl does!)</p> <p>One good use for <code>\d</code> is testing strings to see whether they contain numbers.
For example, you might need to verify that a string contains an
American-style phone number, which has the form <code>555-1212</code>.  You could
use code like this:</p> <pre> unless ($phone =~ /\d\d\d-\d\d\d\d/) {
 print &quot;That's not a phone number!\n&quot;;
    }</pre> <p>All those <code>\d</code> metacharacters make the regex hard to read.
Fortunately, Perl allows us to improve on that.  You can use numbers inside
curly braces to indicate a <em>quantity</em> you want to match, like this:</p> <pre> unless ($phone =~ /\d{3}-\d{4}/) {
 print &quot;That's not a phone number!\n&quot;;
   }</pre> <p>The string <code>\d{3}</code> means to match exactly three numbers, and <code>\d{4}</code> matches exactly four digits.  If you want to use a range of numbers, you
can separate them with a comma; leaving out the second number makes the
range open-ended.  <code>\d{2,5}</code> will match two to five digits, and
&lt;\w{3,}&gt; will match a word that's at least three characters long.</p> <p>You can also <em>invert</em> the <code>\d</code>, <code>\s</code> and <code>\w</code> metacharacters to refer
to anything <em>but</em> that type of character.  <code>\D</code> matches nondigits; <code>\W</code> matches any character that <em>isn't</em> a letter, digit or underscore;
and <code>\S</code> matches anything that isn't whitespace.</p> <p>If these metacharacters won't do what you want, you can define your
own.  You define a character class by enclosing a list of the allowable
characters in square brackets.  For example,
a class containing only the lowercase vowels is <code>[aeiou]</code>.
<code>/b[aeiou]g/</code> will match any string that contains ``bag,'' ``beg,'' ``big,''
``bog'' or ``bug''.  You use dashes to indicate a range of characters,
like <code>[a-f]</code>.  (If Perl didn't give us the <code>\d</code> metacharacter, we could do
the same thing with <code>[0-9]</code>.)  You can combine character classes with
quantifiers:</p> <pre> if ($string =~ /[aeiou]{2}/) {
 print &quot;This string contains at least
        two vowels in a row.\n&quot;;
    }</pre> <p>You can also invert character classes by beginning them with the <code>^</code> character.  An inverted character class will match anything you <em>don't</em> list.  <code>[^aeiou]</code> matches every character except the lowercase vowels.
(Yes, <code>^</code> can also mean ``beginning of string,'' so be careful.)</p> <h3><a name="flags">Flags</a></h3> <p>By default, regular expression matches are case-sensitive (that is,
<code>/bob/</code> doesn't match ``Bob'').  You can place <em>flags</em> after a regexp
to modify their behaviour.  The most commonly used flag is <code>i</code>, which
makes a match case-insensitive:</p> <pre> $greet = &quot;Hey everybody, it's Bob and David!&quot;;
    if ($greet =~ /bob/i) {
        print &quot;Hi, Bob!\n&quot;;
    }</pre> <p>We'll talk about more flags later.</p> <h3><a name="subexpressions">Subexpressions</a></h3> <p>You might want to check for more than one thing at a time.  For example,
you're writing a ``mood meter'' that you use to scan outgoing e-mail for
potentially damaging phrases.  You can use the pipe character <code>|</code> to
separate different things you are looking for:</p> <pre> # In reality, @email_lines would come from your email text, 
   # but here we'll just provide some convenient filler.
   @email_lines = (&quot;Dear idiot:&quot;,
                   &quot;I hate you, you twit.  You're a dope.&quot;,
                   &quot;I bet you mistreat your llama.&quot;,
                   &quot;Signed, Doug&quot;);

   for $check_line (@email_lines) {
       if ($check_line =~ /idiot|dope|twit|llama/) {
           print &quot;Be careful!  This line might
	          contain something offensive:\n&quot;,
                 $check_line, &quot;\n&quot;;
       }
   }</pre> <p>The matching expression <code>/idiot|dope|twit|llama/</code> will be true
if ``idiot,'' ``dope,'' ``twit'' or ``llama'' show up anywhere in the
string.</p> <p>One of the more interesting things you can do with regular expressions
is <em>subexpression matching</em>, or grouping.  A subexpression is like another,
smaller regex buried inside your larger regexp, and is placed inside
parentheses.  The string that caused the subexpression
to match will be stored in the special variable <code>$1</code>.  We can use
this to make our mood meter more explicit about the problems
with your e-mail:</p>  <pre> for $check_line (@email_lines) {
       if ($check_line =~ /(idiot|dope|twit|llama)/) {
           print &quot;Be careful!  This line contains the
                  offensive word $1:\n&quot;,
                 $check_line, &quot;\n&quot;;
       }
   }</pre> <p>Of course, you can put matching expressions in your subexpression.  Your mood
watch program can be extended to prevent you from sending e-mail that
contains more than three exclamation points in a row.  We'll use the special
<code>{3,}</code> quantifier to make sure we get <em>all</em> the exclamation points.</p> <pre> for $check_line (@email_lines) {
        if ($check_line =~ /(!{3,})/) {
            print &quot;Using punctuation like '$1' 
                   is the sign of a sick mind:\n&quot;,
                  $check_line, &quot;\n&quot;;
        }
    }</pre> <p>If your regex contains more than one subexpression, the results will be
stored in variables named <code>$1</code>, <code>$2</code>, <code>$3</code> and so on.  Here's some code
that will change names in ``lastname, firstname'' format back to normal:</p> <pre> $name = &quot;Wall, Larry&quot;;
   $name =~ /(\w+), (\w+)/;
   # $1 contains last name, $2 contains first name

   $name = &quot;$2 $1&quot;;
   # $name now contains &quot;Larry Wall&quot;
</pre> <p>You can even nest subexpressions inside one another - they're ordered as they
open, from left to right.  Here's an example of how to retrieve the full
time, hours, minutes and seconds separately from a string that contains
a timestamp in <code>hh:mm:ss</code> format.  (Notice that we're using the
<code>{1,2}</code> quantifier so that a timestamp like ``9:30:50'' will be matched.)</p> <pre> $string = &quot;The time is 12:25:30 and I'm hungry.&quot;;
    $string =~ /((\d{1,2}):(\d{2}):(\d{2}))/;
    @time = ($1, $2, $3, $4);</pre> <p>Here's a hint that you might find useful: You can assign <em>to</em> a
list of scalar values whenever you're assigning <em>from</em> a list.  If you
prefer to have readable variable names instead of an array, try using this
line instead:</p> <pre> ($time, $hours, $minutes, $seconds) = ($1, $2, $3, $4);</pre> <p>Assigning to a list of variables when you're using subexpressions
happens often enough that Perl gives you a handy shortcut:</p> <pre> ($time, $hours, $minutes, $seconds) =
         ($string =~ /((\d{1,2}):(\d{2}):(\d{2}))/);
</pre> <h3><a name="watch out!">Watch out!</a></h3> <p>Regular expressions have two traps that generate bugs in
your Perl programs: They always start at the beginning of the string,
and quantifiers always match as much of the string as possible.</p> <p>Here's some simple code for counting all the numbers in
a string and showing them to the user.  We'll use <code>while</code> to loop
over the string, matching over and over until we've counted all
the numbers.</p> <pre> $number = &quot;Look, 200 5-sided, 4-colored pentagon maps.&quot;;
    while ($number =~ /(\d+)/) {
        print &quot;I found the number $1.\n&quot;;
        $number_count++;
    }
    print &quot;There are $number_count numbers here.\n&quot;;</pre> <p>This code is actually so simple it doesn't work!  When you run it, Perl
will print <code>I found the number 200</code> over and over again.  Perl
always begins matching at the beginning of the string, so it
will always find the 200, and never get to the following numbers.</p> <p>You can avoid this by using the <code>g</code> flag with your regex.  This flag
will tell Perl to remember where it was in the string when it returns
to it.  When you insert the <code>g</code> flag, our code looks like this:</p> <pre> $number = &quot;Look, 200 5-sided, 4-colored pentagon maps.&quot;;
    while ($number =~ /(\d+)/g) {
        print &quot;I found the number $1.\n&quot;;
        $number_count++;
    }
    print &quot;There are $number_count numbers here.\n&quot;;</pre> <p>Now we get the results we expected:</p> <pre> I found the number 200.
    I found the number 5.
    I found the number 4.
    There are 3 numbers here.</pre> <p>The second trap is that a quantifier will always match as many characters
as it can.  Look at this example code, but don't run it yet:</p> <pre> $book_pref = &quot;The cat in the hat is where it's at.\n&quot;;
    $book_pref =~ /(cat.*at)/;
    print $1, &quot;\n&quot;;
</pre> <p>Take a guess: What's in <code>$1</code> right now?  Now run the code.  Does this
seem counterintuitive?</p> <p>The matching expression <code>(cat.*at)</code> is greedy. It contains
<code>cat in the hat is where it's at</code> because that's the largest string
that matches.  Remember, read left to right: ``cat,'' followed
by any number of characters, followed by ``at.''  If you want to
match the string <code>cat in the hat</code>, you have to rewrite your regexp
so it isn't as greedy.  There are two ways to do this:</p> <p>1. Make the match more precise (try <code>/(cat.*hat)/</code> instead).  Of course,
this still might not work - try using this regexp against <code>The cat in
the hat is who I hate</code>.</p> <p>2. Use a <code>?</code> character after a quantifier to specify nongreedy matching.
<code>.*?</code> instead of <code>.*</code> means that Perl will try to match the <em>smallest</em> string possible instead of the largest:</p> <pre> # Now we get &quot;cat in the hat&quot; in $1.
  $book_pref =~ /(cat.*?at)/;
</pre> <h3><a name="search and replace">Search and replace</a></h3> <p>Now that we've talked about <em>matching</em>, there's one other
thing regular expressions can do for you: <em>replacing</em>.</p> <p>If you've ever used a text editor or word processor, you're familiar
with the search-and-replace function.  Perl's regexp facilities include
something similar, the <code>s///</code> operator, which has the following syntax:
<code>s/regex/replacement string/</code>.  If the string you're testing matches
<em>regex</em>, then whatever matched is replaced with the contents of
<em>replacement string</em>.  For instance, this code will change
a cat into a dog:</p> <pre> $pet = &quot;I love my cat.\n&quot;;
    $pet =~ s/cat/dog/;
    print $pet;</pre> <p>You can also use subexpressions in your matching expression, and use
the variables <code>$1</code>, <code>$2</code> and so on, that they create.  The replacement
string will substitute these, or any other variables, as if it were a
double-quoted string.  Remember our code for changing <code>Wall, Larry</code> into <code>Larry Wall</code>?  We can rewrite it as a single <code>s///</code> statement!</p> <pre> $name = &quot;Wall, Larry&quot;;
    $name =~ s/(\w+), (\w+)/$2 $1/;  # &quot;Larry Wall&quot;</pre> <p><code>s///</code> can take flags, just like matching expressions.  The two most
important flags are <code>g</code> (global) and <code>i</code> (case-insensitive).
Normally, a substitution will only happen <em>once</em>, but specifying the 
<code>g</code> flag will make it happen as long as the regex matches the string.
Try this code, and then remove the <code>g</code> flag and try it again:</p> <pre> $pet = &quot;I love my cat Sylvester, and my other cat Bill.\n&quot;;
   $pet =~ s/cat/dog/g;
   print $pet;</pre> <p>Notice that without the <code>g</code> flag, Bill doesn't turn into a dog.
</p> <p>The <code>i</code> flag works just as it did when we were only using matching
expressions: It forces your matching search to be case-insensitive.</p>  <h3><a name="putting it all together">Putting it all together</a></h3> <p>Regular expressions have many practical uses.  We'll look at a httpd
log analyzer for an example.  In our last article, one of the play-around
items was to write a simple log analyzer.  Now, let's make it a bit more
interesting: a log analyzer that will break down your log results by
file type and give you a list of total requests by hour.</p> <p>(<a href="/2000/11/a3-httpd.pl">Complete source code</a>.)</p>  <p>First, let's look at a sample line from a httpd log:</p> <pre> 127.12.20.59 - - [01/Nov/2000:00:00:37 -0500] 
	&quot;GET /gfx2/page/home.gif HTTP/1.1&quot; 200 2285
</pre> <p>The first thing we want to do is split this into fields.  Remember that
the <code>split()</code> function takes a regular expression as its first argument.
We'll use <code>/\s/</code> to split the line at each whitespace character:</p> <pre> @fields = split(/\s/, $line);</pre> <p>This gives us 10 fields.  The ones we're concerned
with are the fourth field (time and date of request), the seventh (the
URL), and the ninth and 10th (HTTP status code and size in bytes of
the server response).</p> <p>First, we'd like to make sure that we turn any request for a URL that
ends in a slash (like <code>/about/</code>) into a request for the index page from
that directory (<code>/about/index.html</code>).  We'll need to escape out the
slashes so that Perl doesn't mistake them for terminators in our <code>s///</code> statement.</p> <pre> $fields[6] =~ s/\/$/\/index.html/;</pre> <p>This line is difficult to read, because anytime we come across a literal
slash character we need to escape it out.  This problem is so
common, it has acquired a name: <em>leaning-toothpick syndrome</em>.  Here's a useful
trick for avoiding the leaning-toothpick syndrome: You can replace the slashes
that mark regular expressions and <code>s///</code> statements with any other matching
pair of characters, like <code>{</code> and <code>}</code>.  This allows us to write a 
more legible regex where we don't need to escape out the slashes:</p> <pre> $fields[6] =~ s{/$}{/index.html};</pre> <p>(If you want to use this syntax with a matching expression, you'll need
to put a <code>m</code> in front of it.  <code>/foo/</code> would be rewritten as <code>m{foo}</code>.)</p> <p>Now, we'll assume that any URL request that returns a status code of 200
(request OK) is a request for the file type of the URL's extension (a
request for <code>/gfx/page/home.gif</code> returns a GIF image).  Any URL request
without an extension returns a plain-text file.  Remember that the
period is a metacharacter, so we need to escape it out!</p> <pre> if ($fields[8] eq '200') {
           if ($fields[6] =~ /\.([a-z]+)$/i) {
               $type_requests{$1}++;
           } else {
               $type_requests{'txt'}++;
           }
        }</pre> <p>Next, we want to retrieve the <em>hour</em> each request took place.  The hour
is the first string in <code>$fields[3]</code> that will be two digits surrounded
by colons, so all we need to do is look for that.  Remember that Perl
will stop when it finds the first match in a string:</p> <pre> # Log the hour of this request
        $fields[3] =~ /:(\d{2}):/;
        $hour_requests{$1}++;</pre> <p>Finally, let's rewrite our original <code>report()</code> sub.  We're doing the
same thing over and over (printing a section header and the contents
of that section), so we'll break that out into a new sub.  We'll
call the new sub <code>report_section()</code>:</p> <pre> sub report {
    print ``Total bytes requested: '', $bytes, ``\n'';</p> print &quot;\n&quot;;
    report_section(&quot;URL requests:&quot;, %url_requests);
    report_section(&quot;Status code results:&quot;, %status_requests);
    report_section(&quot;Requests by hour:&quot;, %hour_requests);
    report_section(&quot;Requests by file type:&quot;, %type_requests);
}
</pre> <p>The new <code>report_section()</code> sub is very simple:</p> <pre> sub report_section {
    my ($header, %type) = @_;</p> print $header, &quot;\n&quot;;
    for $i (sort keys %type) {
        print $i, &quot;: &quot;, $type{$i}, &quot;\n&quot;;
    }

    print &quot;\n&quot;;
}
</pre> <p>We use the <code>keys</code> function to return a list of the keys in the <code>%type</code> hash, and the <code>sort</code> function to put it in alphabetic order.  We'll
play with <code>sort</code> a bit more in the next article.</p> <h3><a name="play around!">Play around!</a></h3> <p>As usual, here are some sample exercises:</p> <p>1.  A rule of good writing is ``avoid the passive voice.''
Instead of <em>The report was read by Carl</em>, 
say <em>Carl read the report</em>.  Write a program that reads a file of sentences
(one per line), detects and eliminates the passive voice, and prints
the result.  (Don't worry about irregular verbs or capitalization, though.)</p> <p><a href="/2000/11/a3-activate.pl">Sample solution</a>.  
<a href="/2000/11/a3-sentences.txt">Sample test sentences</a>.</p>  <p>2.  You have a list of phone numbers.  The list is messy, and the only
thing you know is that there are either seven or 10 digits in each number
(the area code is optional), and if there's an extension, it will show up
after an ``x'' somewhere on the line.  ``416 555-1212,'' ``5551300X40'' and
``(306) 555.5000 ext 40'' are all possible.  Write a <code>fix_phone()</code> sub that
will turn all of these numbers into the standard format ``(123) 555-1234'' or
``(123) 555-1234 Ext 100,'' if there is an extension.  Assume that the
default area code is ``123.''</p> <p><a href="/2000/11/a3-phone.pl">Sample solution</a>.</p>
