
<!-- The other two Rs -->


<br />
<!::field::date::><br /><p><table width="180" cellspacing="0" cellpadding="4" align="right" border="1">
<tr>
<td align="center" valign="top" bgcolor="#6699cc">
<font color="#ffffff">Table of Contents</td>
</tr>

<tr>
<td>
<p class="fine">
&#149;<b><a href="http://www.perl.com/pub/2000/10/begperl1.html">Part 1 of
this series</a></b><br />
&#149;<a href="#comparison operators">Comparison operators</a><br />
&#149;<a href="#while and until"><code>while</code> and <code>until</code></a><br />
&#149;<a href="#string comparisons">String comparisons</a><br />
&#149;<a href="#more fun with strings">More fun with strings</a><br />
&#149;<a href="#filehandles">Filehandles</a><br />
&#149;<a href="#writing files">Writing files</a><br />
&#149;<a href="#live free or die!">Live free or die!</a><br />
&#149;<a href="#subs">Subs</a><br />
&#149;<a href="#putting it all together">Putting it all together</a><br />
&#149;<a href="#play around!">Play around!</a><br />
           
</td>
</tr>
</p>
</table>

<p>In our last article, we talked about the core elements of Perl:
variables (scalars, arrays, and hashes), math operators and some
basic flow control (the <code>for</code> statement).  Now it's time to interact
with the world.</p>
<p>In this installment, we're going to discuss how to slice and dice strings,
how to play with files and how to define your own functions.  But first,
we'll discuss one more core concept of the Perl language: conditions
and comparisons.</p>
<p>
<h3><a name="comparison operators">Comparison operators</a></h3>
<p>There's one important element of Perl that we skipped in the last
article: <em>comparison operators</em>.  Like all good programming languages, Perl
allows you ask questions such as ``Is this number greater than that number?''
or ``Are these two strings the same?'' and do different things depending
on the answer.</p>
<p>When you're dealing with numbers, Perl has four important operators:
<code>&lt;</code>, <code>&gt;</code>, <code>==</code> and <code>!=</code>.  These are the ``less than,'' ``greater
than,'' ``equal to'' and ``not equal to'' operators.  (You can also use <code>&lt;=</code>,
``less than or equal to,'' and <code>&gt;=</code>, ``greater than or equal to.)</p>
<p>You can use these operators along with one of Perl's <em>conditional</em> keywords,
such as <code>if</code> and <code>unless</code>.  Both of these keywords take a condition that Perl
will test, and a block of code in curly brackets that Perl will run if the
test works.  These two words work just like their English equivalents - an
<code>if</code> test succeeds if the condition turns out to be true, and an <code>unless</code>
test succeeds if the condition turns out to be false:</p>
<pre>
    if ($year_according_to_computer == 1900) {
        print &quot;Y2K has doomed us all!  Everyone to the compound.\n&quot;;
    }

    unless ($bank_account &gt; 0) {
        print &quot;I'm broke!\n&quot;;
    }</pre>
<p>Be careful of the difference between <code>=</code> and <code>==</code>!  One equals sign means
``assignment'', two means ``comparison for equality''.  This is a common,
evil bug:</p>
<pre>
    if ($a = 5) {
        print &quot;This works - but doesn't do what you want!\n&quot;;
    }</pre>
<p>Instead of testing whether <code>$a</code> is equal to five, you've made <code>$a</code> equal
to five and clobbered its old value.  (In a later article, we'll discuss a
way to make sure this bug won't occur in running code.)</p>
<p>Both <code>if</code> and <code>unless</code> can be followed by an <code>else</code> statement and
code block, which executes if your test failed.  You can also use <code>elsif</code>
to chain together a bunch of <code>if</code> statements:</p>
<pre>
    if ($a == 5) {
        print &quot;It's five!\n&quot;;
    } elsif ($a == 6) {
        print &quot;It's six!\n&quot;;
    } else {
        print &quot;It's something else.\n&quot;;
    }

    unless ($pie eq 'apple') {
        print &quot;Ew, I don't like $pie flavored pie.\n&quot;;
    } else {
        print &quot;Apple!  My favorite!\n&quot;;
    }</pre>
<p>
<h3><a name="while and until"><code>while</code> and <code>until</code></a></h3>
<p>Two slightly more complex keywords are <code>while</code> and <code>until</code>.
They both take a condition and a block of code, just like <code>if</code> and <code>unless</code>,
but they act like loops similar to <code>for</code>.  Perl tests the condition, runs the
block of code and runs it over and over again for as long as the condition
is true (for a <code>while</code> loop) or false (for a <code>until</code> loop).</p>
<p>Take a look at the following code and try to guess what it
will do before reading further:</p>
<pre>
   $a = 0;

   while ($a != 3) {
       $a++;
       print &quot;Counting up to $a...\n&quot;;
   }

   until ($a == 0) {
       $a--;
       print &quot;Counting down to $a...\n&quot;;
   }</pre>
<p>Here's what you see when you run this program:</p>
<pre>
    Counting up to 1...
    Counting up to 2...
    Counting up to 3...
    Counting down to 2...
    Counting down to 1...
    Counting down to 0...</pre>
<p>
<h3><a name="string comparisons">String comparisons</a></h3>
<p>So that's how you compare numbers.  Now, what about strings?  The most
common string comparison operator is <code>eq</code>, which tests for <em>string
equality</em> - that is, whether two strings have the same value.</p>
<p>Remember the pain that is caused when you mix up <code>=</code> and <code>==</code>?  Well,
you can also mix up <code>==</code> and <code>eq</code>.  This is one of the few cases where
it <em>does</em> matter whether Perl is treating a value as a string or a number.
Try this code:</p>
<pre>
    $yes_no = &quot;no&quot;;
    if ($yes_no == &quot;yes&quot;) {
        print &quot;You said yes!\n&quot;;
    }</pre>
<p>Why does this code think you said yes?  Remember that Perl automatically
converts strings to numbers whenever it's necessary; the <code>==</code> operator
implies that you're using numbers, so Perl converts the value of <code>$yes_no</code>
(``no'') to the number 0, and ``yes'' to the number 0 as well.  Since this
equality test works (0 is equal to 0), the <code>if</code> block gets run.  Change
the condition to <code>$yes_no eq &quot;yes&quot;</code>, and it'll do what it should.</p>
<p>Things can work the other way, too.  The number five is <em>numerically</em>
equal to the string <code>&quot; 5 &quot;</code>, so comparing them to <code>==</code> works.  But when
you compare five and <code>&quot; 5 &quot;</code> with <code>eq</code>, Perl will convert the number to
the string <code>&quot;5&quot;</code> first, and then ask whether the two strings have the same value.
Since they don't, the <code>eq</code> comparison fails.  This code fragment will
print <code>Numeric equality!</code>, but not <code>String equality!</code>:</p>
<pre>
    $a = 5;
    if ($a == &quot; 5 &quot;) { print &quot;Numeric equality!\n&quot;; }
    if ($a eq &quot; 5 &quot;) { print &quot;String equality!\n&quot;; }</pre>
<p>
<h3><a name="more fun with strings">More fun with strings</a></h3>
<p>You'll often want to manipulate strings: Break them into smaller pieces,
put them together and change their contents.  Perl offers
three functions that make string manipulation easy and fun:  <code>substr()</code>,
<code>split()</code> and <code>join()</code>.</p>
<p>If you want to retrieve part of a string (say, the first four
characters or a 10-character chunk from the middle), use the
<code>substr()</code> function.  It takes either two or three parameters: the
string you want to look at, the character position to start at (the
first character is position 0) and the number of
characters to retrieve.  If you leave out the number of characters,
you'll retrieve everything up to the end of the string.</p>
<pre>
    $a = &quot;Welcome to Perl!\n&quot;;
    print substr($a, 0, 7);     # &quot;Welcome&quot;
    print substr($a, 7);        # &quot; to Perl!\n&quot;</pre>
<p>A neat and often-overlooked thing about <code>substr()</code> is that you can use
a <em>negative</em> character position.  This will retrieve a substring
that begins with many characters from the <em>end</em> of the string.</p>
<pre>
     $a = &quot;Welcome to Perl!\n&quot;;
     print substr($a, -6, 4);      # &quot;Perl&quot;</pre>
<p>(Remember that inside double quotes, <code>\n</code> represents the single new-line
character.)</p>
<p>You can also manipulate the string by using <code>substr()</code> to assign a
new value to part of it.  One useful trick is using a length of zero to
<em>insert</em> characters into a string:</p>
<pre>
    $a = &quot;Welcome to Java!\n&quot;;
    substr($a, 11, 4) = &quot;Perl&quot;;   # $a is now &quot;Welcome to Perl!\n&quot;;
    substr($a, 7, 3) = &quot;&quot;;        #       ... &quot;Welcome Perl!\n&quot;;
    substr($a, 0, 0) = &quot;Hello. &quot;; #       ... &quot;Hello. Welcome Perl!\n&quot;;</pre>
<p>Next, let's look at <code>split()</code>.  This function breaks apart a
string and returns a list of the pieces.  <code>split()</code> generally
takes two parameters: a <em>regular expression</em> to split the string
with and the string you want to split.  (We'll discuss regular
expressions in more detail in the next article; for the moment, we're
only going to use a space.  Note the special syntax for a regular
expression: <code>/ /</code>.)  The characters you split won't show up in
any of the list elements.</p>
<pre>
    $a = &quot;Hello. Welcome Perl!\n&quot;;
    @a = split(/ /, $a);   # Three items: &quot;Hello.&quot;, &quot;Welcome&quot;, &quot;Perl!\n&quot;</pre>
<p>You can also specify a third parameter: the maximum number of items to
put in your list.  The splitting will stop as soon as your list
contains that many items:</p>
<pre>
    $a = &quot;Hello. Welcome Perl!\n&quot;;
    @a = split(/ /, $a, 2);   # Two items: &quot;Hello.&quot;, &quot;Welcome Perl!\n&quot;;</pre>
<p>Of course, what you can split, you can also <code>join()</code>.  The <code>join()</code>
function takes a list of strings and attaches them together with a
specified string between each element, which may be an empty string:</p>
<pre>
    @a = (&quot;Hello.&quot;, &quot;Welcome&quot;, &quot;Perl!\n&quot;);
    $a = join(' ', @a);       # &quot;Hello. Welcome Perl!\n&quot;;
    $b = join(' and ', @a);   # &quot;Hello. and Welcome and Perl!\n&quot;;
    $c = join('', @a);        # &quot;Hello.WelcomePerl!\n&quot;;</pre>
<p>
<h3><a name="filehandles">Filehandles</a></h3>
<p>Enough about strings.  Let's look at files - after all, what good is
string manipulation if you can't do it where it counts?</p>
<p>To read from or write to a file, you have to <em>open</em> it.  When you
open a file, Perl asks the operating system if the file can be accessed -
does the file exist if you're trying to read it (or can it be created
if you're trying to create a new file), and do you have the necessary
file permissions to do what you want?  If you're allowed to use the file,
the operating system will prepare it for you, and Perl will give you
a <em>filehandle</em>.</p>
<p>You ask Perl to create a filehandle for you by using the <code>open()</code> function,
which takes two arguments: the filehandle you want to create and the file
you want to work with.  First, we'll concentrate on reading files.  The
following statement opens the file <code>log.txt</code> using the filehandle <code>LOGFILE</code>:</p>
<pre>
    open (LOGFILE, &quot;log.txt&quot;);</pre>
<p>Opening a file involves several behind-the-scenes tasks that Perl and
the operating system undertake together, such as checking that the file you
want to open actually exists (or creating it if you're trying to create
a new file) and making sure you're allowed to manipulate the file (do you
have the necessary file permissions, for instance).  Perl will do all
of this for you, so in general you don't need to worry about it.</p>
<p>Once you've opened a file to read, you can retrieve lines from it by
using the <code>&lt;&gt;</code> construct.  Inside the angle brackets, place
the name of your filehandle.  What is returned by this depends on what
you <em>want</em> to get: in a scalar context (a more technical way of
saying ``if you're assigning it to a scalar''), you retrieve the next
line from the file, but if you're looking for a list, you get a list of
all the remaining lines in the file.  (One common trick is to use
<code>for $lines (&lt;FH&gt;)</code> to retrieve all the lines from a file -
the <code>for</code> means you're asking a list.)</p>
<p>You can, of course, <code>close</code> a filehandle that you've opened.  You don't
always have to do this, because Perl is clever enough to close a filehandle
when your program ends or when you try to reuse an existing filehandle.
It's a good idea, though, to use the <code>close</code> statement.  Not only will
it make your code more readable, but your operating system has
built-in limits on the number of files that can be open at once, and
each open filehandle will take up valuable memory.</p>
<p>Here's a simple program that will display the contents of the file
<code>log.txt</code>, and assumes that the first line of the file is its title:</p>
<pre>
    open (LOGFILE, &quot;log.txt&quot;) or die &quot;I couldn't get at log.txt&quot;;
    # We'll discuss the &quot;or die&quot; in a moment.

    $title = &lt;LOGFILE&gt;;
    print &quot;Report Title: $title&quot;;
    for $line (&lt;LOGFILE&gt;) {
        print $line;
    }
    close LOGFILE;</pre>
<p>
<h3><a name="writing files">Writing files</a></h3>
<p>You also use <code>open()</code> when you are writing to a file.  There are two
ways to open a file for writing: <em>overwrite</em> and <em>append</em>.
When you open a file in overwrite mode, you erase whatever it
previously contained.  In append mode, you attach your new data to the
end of the existing file without erasing anything that was already
there.</p>
<p>To indicate that you want a filehandle for writing, you put a single
<code>&gt;</code> character before the filename you want to use.  This opens
the file in overwrite mode.  To open it in append mode, use two
<code>&gt;</code> characters.</p>
<pre>
     open (OVERWRITE, &quot;&gt;overwrite.txt&quot;) or die &quot;$! error trying to overwrite&quot;;
     # The original contents are gone, wave goodbye.

     open (APPEND, &quot;&gt;&gt;append.txt&quot;) or die &quot;$! error trying to append&quot;;
     # Original contents still there, we're adding to the end of the file</pre>
<p>Once our filehandle is open, we can use the humble <code>print</code> statement
to write to it.  Specify the filehandle you want to write to and a
list of values you want to write:</p>
<pre>
    print OVERWRITE &quot;This is the new content.\n&quot;;
    print APPEND &quot;We're adding to the end here.\n&quot;, &quot;And here too.\n&quot;;</pre>
<p>
<h3><a name="live free or die!">Live free or die!</a></h3>
<p>You noticed that most of our <code>open()</code> statements are followed by
<code>or die &quot;some sort of message&quot;</code>.  This is because we live in an
imperfect world, where programs don't always behave exactly the way we
want them to.  It's always possible for an <code>open()</code> call to fail;
maybe you're trying to write to a file that you're not allowed to write,
or you're trying to read from a file that doesn't exist.  In Perl, you
can guard against these problems by using <code>or</code> and <code>and</code>.</p>
<p>A series of statements separated by <code>or</code> will continue until you hit
one that works, or returns a true value.  This line of code will
either succeed at opening <code>OUTPUT</code> in overwrite mode, or cause Perl
to quit:</p>
<pre>
    open (OUTPUT, &quot;&gt;$outfile&quot;) or die &quot;Can't write to $outfile: $!&quot;;</pre>
<p>The <code>die</code> statement ends your program with an error message.  The
special variable <code>$!</code> contains Perl's explanation of the error.  In
this case, you might see something like this if you're not allowed
to write to the file.  Note that you get both the actual error message
(``Permission denied'') and the line where it happened:</p>
<pre>
    Can't write to a2-die.txt: Permission denied at ./a2-die.pl line 1.</pre>
<p>Defensive programming like this is useful for making your programs more
error-resistant - you don't want to write to a file that you haven't
successfully opened!</p>
<p>Here's an example: As part of your job, you write a program that records
its results in a file called <code>vitalreport.txt</code>.  You use the following code:</p>
<pre>
    open VITAL, &quot;&gt;vitalreport.txt&quot;;</pre>
<p>If this <code>open()</code> call fails (for instance, <code>vitalreport.txt</code> is owned
by another user who hasn't given you write permission), you'll never
know it until someone looks at the file afterward and wonders why the
vital report wasn't written.  (Just imagine the joy if that ``someone'' is
your boss, the day before your annual performance review.)  When you use
<code>or die</code>, you avoid all this:</p>
<pre>
    open VITAL, &quot;&gt;vitalreport.txt&quot; or die &quot;Can't write vital report: $!&quot;;</pre>
<p>Instead of wondering whether your program wrote your vital report,
you'll immediately have an error message that both tells you what
went wrong and on what line of your program the error occurred.</p>
<p>You can use <code>or</code> for more than just testing file operations:</p>
<pre>
    ($pie eq 'apple') or ($pie eq 'cherry') or ($pie eq 'blueberry')
        or print &quot;But I wanted apple, cherry, or blueberry!\n&quot;;</pre>
<p>In this sequence, if you have an appropriate pie, Perl skips the rest
of the chain.  Once one statement works, the rest are ignored.
The <code>and</code> operator does the opposite: It evaluates your chain of
statements, but stops when one of them <em>doesn't</em> work.</p>
<pre>
   open (LOG, &quot;log.file&quot;) and print &quot;Logfile is open!\n&quot;;</pre>
<p>This statement will only show you the words <em>Logfile is open!</em> if the
<code>open()</code> succeeds - do you see why?</p>
<p>
<h3><a name="subs">Subs</a></h3>
<p>So far, our Perl programs have been a bunch of statements in series.
This is OK if you're writing very small programs, but as your needs
grow, you'll find it's limiting.  This is why most modern programming
languages allow you to define your own functions; in Perl, we call them
<em>subs</em>.</p>
<p>A sub is defined with the <code>sub</code> keyword, and adds a new function to
your program's capabilities.  When you want to use this new function,
you call it by name.  For instance, here's a short definition of a sub
called <code>boo</code>:</p>
<pre>
    sub boo {
        print &quot;Boo!\n&quot;;
    }

    boo();   # Eek!</pre>
<p>(Older versions of Perl required that you precede the name of a sub with the
<code>&amp;</code> character when you call it.  You no longer have to do this, but if you
see code that looks like <code>&amp;boo</code> in other people's Perl, that's why.)</p>
<p>Subs are useful because they allow you to break your program into
small, reusable chunks.  If you need to analyze a string in four
different places in your program, it's much easier to write one
<code>&amp;analyze_string</code> sub and call it four times.  This way,
when you make an improvement to your string-analysis routine,
you'll only need to do it in one place, instead of four.</p>
<p>In the same way that Perl's built-in functions can take parameters and
can return values, your subs can, too.  Whenever you call a sub, any
parameters you pass to it are placed in the special array <code>@_</code>.  You
can also return a single value or a list by using the <code>return</code>
keyword.</p>
<pre>
    sub multiply {
        my (@ops) = @_;
        return $ops[0] * $ops[1];
    }

    for $i (1 .. 10) {
         print &quot;$i squared is &quot;, multiply($i, $i), &quot;\n&quot;;
    }</pre>
<p>Why did we use the <code>my</code> keyword?  That indicates that the variables
are private to that sub, so that any existing value for the <code>@ops</code> array
we're using elsewhere in our program won't get overwritten.  This
means that you'll evade a whole class of hard-to-trace bugs in your
programs.  You don't <em>have</em> to use <code>my</code>, but you also don't <em>have</em> to
avoid smashing your thumb when you're hammering nails into a board.
They're both just good ideas.</p>
<p>You can also use <code>my</code> to set up local variables in a sub without
assigning them values right away.  This can be useful for loop indexes
or temporary variables:</p>
<pre>
    sub annoy {
        my ($i, $j);
        for $i (1 .. 100) {
            $j .= &quot;Is this annoying yet?\n&quot;;
        }
        print $j;
    }</pre>
<p>If you don't expressly use the <code>return</code> statement, the sub returns
the result of the last statement.  This implicit return value can sometimes
be useful, but it does reduce your program's readability.  Remember
that you'll read your code many more times than you write it!</p>
<p>
<h3><a name="putting it all together">Putting it all together</a></h3>
<p>At the end of the first article we had a simple interest calculator.
Now let's make it a bit more interesting by writing our interest
table to a file instead of to the screen.  We'll also break our code
into subs to make it easier to read and maintain.</p>

<p><a href="http://www.perl.com/2000/11/compound_interest_file.pl">[Download
this program]</a></p>

<pre>
        #!/usr/local/bin/perl -w



        # compound_interest_file.pl - the miracle of compound interest, part 2


        # First, we'll set up the variables we want to use.
        $outfile = &quot;interest.txt&quot;;  # This is the filename of our report.
        $nest_egg = 10000;          # $nest_egg is our starting amount
        $year = 2000;               # This is the starting year for our table.
        $duration = 10;             # How many years are we saving up?
        $apr = 9.5;                 # This is our annual percentage rate.


        &amp;open_report;
        &amp;print_headers;
        &amp;interest_report($nest_egg, $year, $duration, $apr);
        &amp;report_footer;


        sub open_report {
            open (REPORT, &quot;&gt;$outfile&quot;) or die &quot;Can't open report: $!&quot;;
        }


        sub print_headers {
            # Print the headers for our report.
            print REPORT &quot;Year&quot;, &quot;\t&quot;, &quot;Balance&quot;, &quot;\t&quot;, &quot;Interest&quot;, &quot;\t&quot;,
                         &quot;New balance&quot;, &quot;\n&quot;;
        }


        sub calculate_interest {
            # Given a nest egg and an APR, how much interest do we collect?
            my ($nest_egg, $apr) = @_;


            return int (($apr / 100) * $nest_egg * 100) / 100;
        }


        sub interest_report {
            # Get our parameters.  Note that these variables won't clobber the
            # global variables with the same name.
            my ($nest_egg, $year, $duration, $apr) = @_;


            # We have two local variables, so we'll use my to declare them here.
            my ($i, $line);


            # Calculate interest for each year.
            for $i (1 .. $duration) {
                $year++;
                $interest = &amp;calculate_interest($nest_egg, $apr);


                $line = join(&quot;\t&quot;, $year, $nest_egg, $interest,
                             $nest_egg + $interest) . &quot;\n&quot;;


                print REPORT $line;

                $nest_egg += $interest;
            }
        }

        sub report_footer {
            print REPORT &quot;\n Our original assumptions:\n&quot;;
            print REPORT &quot;   Nest egg: $nest_egg\n&quot;;
            print REPORT &quot;   Number of years: $duration\n&quot;;
            print REPORT &quot;   Interest rate: $apr\n&quot;;

            close REPORT;
        }

</pre>
<p>Notice how much clearer the program logic becomes when you break it down
into subs.  One nice quality of a program written as small, well-named
subs is that it almost becomes <em>self-documenting</em>.  Take a look
at these four lines from our program:</p>
<pre>
     open_report;
     print_headers;
     interest_report($nest_egg, $year, $duration, $apr);
     report_footer;
</pre>
<p>Code like this is invaluable when you come back to it six months later
and need to figure out what it does - would you rather spend your time
reading the entire program trying to figure it out or read four lines that
tell you the program 1) opens a report file, 2) prints some headers,
3) generates an interest report, and 4) prints a report footer?</p>
<p>You'll also notice we use <code>my</code> to set up local variables in the
<code>interest_report</code> and <code>calculate_interest</code> subs.  The value of
<code>$nest_egg</code> in the main program never changes.  This is useful at the
end of the report, when we output a footer containing our original
assumptions.  Since we never specified a local <code>$nest_egg</code> in
<code>report_footer</code>, we use the global value.</p>
<p>
<h3><a name="play around!">Play around!</a></h3>
<p>In this article, we've looked at files (filehandles, <code>open()</code>, <code>close()</code>,
and <code>&lt;&gt;</code>), string manipulation (<code>substr()</code>, <code>split()</code> and
<code>join()</code>) and subs.  Here's a pair of exercises - again, one simple and
one complex:</p>
<ul>
<li>
You have a file called <code>dictionary.txt</code> that contains dictionary
definitions, one per line, in the format ``word <code>space</code>
definition''.  (<a href="http://www.perl.com/2000/11/dictionary.txt">Here's a sample</a>.)
Write a program that will look up a word from the command line.
(Hints: <code>@ARGV</code> is a special array that contains your command line
arguments and you'll need to use the three-argument form of
<code>split()</code>.)  Try to enhance it so that your dictionary can also
contain words with multiple definitions in the format ``word <code>space</code>
definition:alternate definition:alternate definition, etc...''.
<p></p>
<li>
Write an analyzer for your Apache logs.  You can find a brief
description of the common log format at
<a href="http://www.w3.org/Daemon/User/Config/Logging.html">http://www.w3.org/Daemon/User/Config/Logging.html</a>.  Your analyzer
should count the total number of requests for each URL, the
total number of results for each status code and the total number of
bytes output.
<p></p></ul>
